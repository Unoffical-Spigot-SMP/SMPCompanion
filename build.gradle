plugins {
    id "java"
    id "io.github.goooler.shadow" version "8.1.7"
}

group = "net.overlordmc"
version = "1"

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "minebench"
        url = "https://repo.minebench.de/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.20.6-R0.1-SNAPSHOT"

    implementation "dev.dejvokep:boosted-yaml:1.3.5"
    implementation "de.themoep:minedown:1.7.1-SNAPSHOT"
}

tasks {
    build {
        dependsOn("shadowJar")
    }
    shadowJar {
        setArchiveClassifier(null)

        relocate "dev.dejvokep.boostedyaml", "net.overlordmc.smpcompanion.lib.boostedyaml"
        relocate "de.themoep.minedown", "net.overlordmc.smpcompanion.lib.minedown"
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}
